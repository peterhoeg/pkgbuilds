# Maintainer: Peter Hoeg <firstname at lastname dot com>

_pkgbase=dynamic-colors
pkgname=${_pkgbase}-git
pkgver=r27.8c537d1
pkgrel=1
pkgdesc='Dynamic colors in Bash and ZSH'
arch=('any')
url="https://github.com/peterhoeg/${_pkgbase}"
license=('GPL')
source=("git+${url}.git")
depends=('bash')
sha1sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_pkgbase}
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
      printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

package() {
  cd ${srcdir}/${_pkgbase}
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/dynamic-colors
  mkdir -p ${pkgdir}/usr/share/bash-completion/completions
  mkdir -p ${pkgdir}/usr/share/zsh/site-functions
  install -m755 bin/dynamic-colors ${pkgdir}/usr/bin/dynamic-colors
  install -dm755                   ${pkgdir}/usr/share/dynamic-colors/colorschemes
  install -m644 colorschemes/*     ${pkgdir}/usr/share/dynamic-colors/colorschemes/
  install -m644 completions/dynamic-colors.bash ${pkgdir}/usr/share/bash-completion/completions/dynamic-colors
  install -m644 completions/dynamic-colors.zsh  ${pkgdir}/usr/share/zsh/site-functions/_dynamic-colors
}

# vim:set ts=2 sw=2 et:
